apply plugin: 'com.android.library'
apply plugin: "com.jfrog.artifactory"
apply plugin: "maven-publish"

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/base'
        main.java.srcDirs += 'src/main/api16'
        main.java.srcDirs += 'src/main/api21'
        main.java.srcDirs += 'src/main/types'
        main.java.srcDirs += 'src/main/utils'
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:25.3.1'

    compile 'android.arch.lifecycle:runtime:1.0.0-alpha1'
    compile 'android.arch.lifecycle:extensions:1.0.0-alpha1'
    annotationProcessor 'android.arch.lifecycle:compiler:1.0.0-alpha1'
}

def libraryGroupId = "lt.farmis.libraries.flurgle"
def libraryArtifactId = "camerakit"
def libraryVersion = "0.10.0"

task androidSourcesJar(type: Jar) {
    classifier = "sources"
    baseName = libraryArtifactId
    from android.sourceSets.main.java.srcDirs
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId libraryGroupId
            version libraryVersion
            artifactId libraryArtifactId
            artifact(androidSourcesJar)
            artifact("$buildDir/outputs/aar/camerakit-release.aar")
        }
    }
}

artifactory {

    Properties localproperties = new Properties()
    localproperties.load(project.rootProject.file("local.properties").newDataInputStream())

    contextUrl = localproperties.getProperty("link_repository")

    publish {
        repository {
            repoKey = "android-libraries"

            username = localproperties.getProperty("artifactory_username")
            password = localproperties.getProperty("artifactory_password")
        }

        defaults {
            publications("aar")
            publishArtifacts = true

            properties = ["qa.level": "basic", "q.os": "android", "dev.team": "core"]
            publishPom = true
        }
    }
}


